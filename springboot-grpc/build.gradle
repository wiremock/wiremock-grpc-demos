buildscript {
	repositories {
		maven {
			url "https://oss.sonatype.org"
		}
		mavenCentral()
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id "com.google.protobuf" version "0.9.4"
}

group = 'org.wiremock.demo'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenLocal()
	mavenCentral()
}

ext {
	versions = [
		wiremock    : "3.10.0",
		wiremockGrpc: "0.9.0",
		grpc        : "1.68.0",
		jackson     : '2.17.2',
	]
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation 'org.springframework.grpc:spring-grpc-spring-boot-starter:0.2.0'

	implementation platform("com.fasterxml.jackson:jackson-bom:$versions.jackson")

	implementation "io.grpc:grpc-bom:$versions.grpc"
	implementation "io.grpc:grpc-protobuf:$versions.grpc"
	implementation "io.grpc:grpc-stub:$versions.grpc"
	implementation "io.grpc:grpc-okhttp:$versions.grpc"

	implementation 'javax.annotation:javax.annotation-api:1.3.2'

	protobuf 'com.google.protobuf:protobuf-java:3.18.1'
	protobuf 'io.grpc:protoc-gen-grpc-java:1.42.1'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	testImplementation "org.wiremock:wiremock-grpc-extension-jetty12:$versions.wiremockGrpc"
	testImplementation "org.wiremock.integrations:wiremock-spring-boot:3.0.0"

}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.24.3"
	}

	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:$versions.grpc"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {
				outputSubDir = 'java'
			}
		}

		all().each { task ->
			task.generateDescriptorSet = true
			task.descriptorSetOptions.includeImports = true
			task.descriptorSetOptions.path = "$projectDir/src/test/resources/grpc/services.dsc"
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

processTestResources.dependsOn generateProto
processTestResources.dependsOn generateTestProto

